
# Maintainer: fincs <fincs.alt1@gmail.com>
pkgname=switch-bulletphysics
pkgver=2.87
pkgrel=2
pkgdesc='Bullet Physics SDK: real-time collision detection and multi-physics simulation for VR, games, visual effects, robotics, machine learning etc. (for Nintendo Switch homebrew development)'
arch=('any')
url='http://bulletphysics.org/'
license=('apache')
options=(!strip libtool staticlibs)
source=( "https://github.com/bulletphysics/bullet3/archive/${pkgver}.tar.gz" "bulletphysics.patch")
makedepends=('devkitA64' 'devkitpro-pkgbuild-helpers')
sha256sums=(
  '438c151c48840fe3f902ec260d9496f8beb26dba4b17769a4a53212903935f95'
  '6c91f465dc2c3d2533cf58fba8e82cbca905547638e189d050ff0637303d84e7')
groups=('switch-portlibs')
build() {
  cd bullet3-$pkgver

  source /opt/devkitpro/switchvars.sh

  patch -Np1 -i "$srcdir"/bulletphysics.patch

  cmake -G"Unix Makefiles" -DCMAKE_TOOLCHAIN_FILE=/opt/devkitpro/switch.cmake \
    -DCMAKE_INSTALL_PREFIX=$PORTLIBS_PREFIX \
    -DCMAKE_C_FLAGS="$CFLAGS $CPPFLAGS" \
    -DCMAKE_CXX_FLAGS="$CFLAGS -fno-exceptions -fno-rtti" \
    -DCMAKE_AR="$DEVKITPRO/devkitA64/bin/aarch64-none-elf-gcc-ar" \
    -DBUILD_CPU_DEMOS:BOOL=OFF -DBUILD_BULLET3:BOOL=OFF -DBUILD_OPENGL3_DEMOS:BOOL=OFF -DBUILD_BULLET2_DEMOS:BOOL=OFF -DBUILD_EXTRAS:BOOL=OFF -DBUILD_UNIT_TESTS:BOOL=OFF \
    .

  make

}

package() {

  cd bullet3-$pkgver

  source /opt/devkitpro/switchvars.sh

  make install DESTDIR="$pkgdir"

}
