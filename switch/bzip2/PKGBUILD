
# Maintainer: Dave Murphy <davem@devkitpro.org>

pkgname=('switch-bzip2')
pkgver=1.0.6
pkgrel=2
pkgdesc="A high-quality data compression program"
arch=('any')
license=('custom')
url="http://www.bzip.org"
options=(!strip libtool staticlibs)
source=(http://http.debian.net/debian/pool/main/b/bzip2/bzip2_$pkgver.orig.tar.bz2)
sha256sums=('a2848f34fcd5d6cf47def00461fcb528a0484d8edef8208d6d2e2909dc61d9cd')
makedepends=('switch-pkg-config' 'devkitpro-pkgbuild-helpers')

build() {

  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  make -C "$srcdir/bzip2-$pkgver" \
  CC=${TOOL_PREFIX}gcc \
  AR=${TOOL_PREFIX}ar \
  RANLIB=${TOOL_PREFIX}ranlib \
  CPPFLAGS="${CPPFLAGS}" \
  CFLAGS="-D_FILE_OFFSET_BITS=64 -Winline ${CFLAGS}" libbz2.a

}

package() {
  source /opt/devkitpro/devkita64.sh
  source /opt/devkitpro/switchvars.sh

  install -Dm 644 "$srcdir/bzip2-$pkgver"/LICENSE -t "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}"
  install -Dm 644 "$srcdir/bzip2-$pkgver"/bzlib.h -t "${pkgdir}/${PORTLIBS_PREFIX}/include/"
  install -Dm 644 "$srcdir/bzip2-$pkgver"/libbz2.a -t "${pkgdir}/${PORTLIBS_PREFIX}/lib"
}
